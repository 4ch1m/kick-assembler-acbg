plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.3'
    id 'org.jetbrains.grammarkit' version '2021.1.3'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }
}

intellij {
    version '2021.2.2'
    pluginName 'kick-assembler-acbg'
    updateSinceUntilBuild false

    patchPluginXml {
        sinceBuild '201.0'
    }
}

publishPlugin {
    token intellijPublishToken
}

group 'de.achimonline'
version 1.5

intellij.plugins 'java'

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateLexer(type: GenerateLexer) {
    source 'src/main/java/de/achimonline/kickassembler/acbg/lexer/KickAssembler.flex'
    targetDir 'src/main/gen/de/achimonline/kickassembler/acbg/lexer/'
    targetClass 'KickAssemblerLexer'
    purgeOldFiles true
}

task generateParser(type: GenerateParser) {
    source 'src/main/java/de/achimonline/kickassembler/acbg/psi/KickAssembler.bnf'
    targetRoot 'src/main/gen'
    pathToParser '/de/achimonline/kickassembler/acbg/parser/KickAssemblerParser.java'
    pathToPsiRoot '/de/achimonline/kickassembler/acbg/psi'
    purgeOldFiles true
}

compileJava {
    dependsOn generateLexer
    dependsOn generateParser
}
